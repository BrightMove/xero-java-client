// Copyright 2014 Connectifier, Inc. All Rights Reserved.

apply plugin: 'eclipse'
apply plugin: 'ivy-publish'
apply plugin: 'java'
apply plugin: 'maven'
apply plugin: 'signing'

group = 'com.connectifier.xero'
version = '0.11'

repositories {
  mavenCentral()
}

dependencies {
  compile 'com.google.guava:guava:18.0'
  compile 'org.bouncycastle:bcpkix-jdk15on:1.51'
  compile 'org.scribe:scribe:1.3.5'
  testCompile 'junit:junit:4.11'
}

sourceSets {
  main {
    java.srcDirs = [ 'src/main/java', 'src/generated/java' ]
  }
}

compileJava.dependsOn ':source-gen:xjc'

// Publish to local ivy repository with: gradle publishIvyJavaPublicationToIvyRepository
publishing {
  publications {
    ivyJava(IvyPublication) {
      from components.java
    }
  }
  repositories {
    ivy {
      url "${System.properties['user.home']}/.ivy2/local"
      layout 'ivy'
    }
  }
}

task sourcesJar(type: Jar, dependsOn: classes) {
  classifier = 'sources'
  from sourceSets.main.allSource
}
 
task javadocJar(type: Jar, dependsOn: javadoc) {
  classifier = 'javadoc'
  from javadoc.destinationDir
}

artifacts {
  archives sourcesJar
  archives javadocJar
}

signing {
  sign configurations.archives
}

uploadArchives {
  repositories.mavenDeployer {
    beforeDeployment { MavenDeployment deployment -> signing.signPom(deployment) }
    repository(url: "https://oss.sonatype.org/service/local/staging/deploy/maven2") {
      authentication(userName: nexusUsername, password: nexusPassword)
    }
    pom.project {
      name 'Xero API Java client library'
      description 'Xero API Java client library'
      url 'https://github.com/connectifier/xero-java-client'
      licenses {
        license {
          name 'The Apache Software License, Version 2.0'
          url 'https://github.com/connectifier/xero-java-client/blob/master/LICENSE'
          distribution 'repo'
        }
      }
      scm {
        url 'https://github.com/connectifier/xero-java-client'
        connection 'git@github.com:connectifier/xero-java-client.git'
      }
      developers {
        developer {
          id 'benmccann'
          name 'Benjamin McCann'
          organization 'connectifier.com'
          organizationUrl 'https://www.connectifier.com/'
        }
      }
    }
  }
}

